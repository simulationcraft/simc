actions+=/skull_bash
actions+=/soothe
actions+=/call_action_list,name=mitigation

actions.mitigation+=/barkskin
actions.mitigation+=/ironfur,if=buff.bear_form.up&tanking&incoming_damage_5s>0.1*health.max&buff.ironfur.remains<2
actions.mitigation+=/regrowth,if=health.pct<65
actions.mitigation+=/renewal,if=health.pct<65
actions.mitigation+=/frenzied_regeneration,if=buff.bear_form.up&buff.frenzied_regeneration.down&health.pct<65
actions.mitigation+=/survival_instincts,if=buff.survival_instincts.down&buff.barkskin.down&health.pct<60&incoming_damage_5s>0.25*health.max


########################################

actions.precombat=flask
actions.precombat+=/food
actions.precombat+=/augmentation

actions.precombat+=/snapshot_stats

actions.precombat+=/bear_form

actions=auto_attack,if=!buff.prowl.up

########################################


actions.precombat+=/mark_of_the_wild
actions.precombat+=/variable,name=ironfur_spam,value=1,value_else=0,if=talent.thorns_of_iron.enabled&talent.reinforced_fur.enabled
actions.precombat+=/variable,name=ripweaving,value=1,value_else=0,if=talent.rip.enabled&talent.fluid_form.enabled&talent.wildpower_surge.enabled
actions.precombat+=/variable,name=use_cat_filler,value=1,value_else=0,if=variable.ripweaving&talent.primal_fury.enabled
actions.precombat+=/heart_of_the_wild,if=!variable.ripweaving


actions+=/use_item,slot=trinket1
actions+=/use_item,slot=trinket2
actions+=/potion,if=(buff.berserk_bear.up|buff.incarnation_guardian_of_ursoc.up)
actions+=/run_action_list,name=cat,if=buff.cat_form.up
actions+=/run_action_list,name=bear


actions.bear+=/moonfire,cycle_targets=1,if=refreshable&target.time_to_die>12&spell_targets.swipe_bear<=3+3*talent.fury_of_nature.enabled
actions.bear+=/thrash_bear,if=refreshable|dot.thrash_bear.stack<3+talent.flashing_claws.rank

actions.bear+=/pulverize
actions.bear+=/barkskin
actions.bear+=/bristling_fur,if=(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>8)

actions.bear+=/heart_of_the_wild,if=!variable.ripweaving
actions.bear+=/convoke_the_spirits,if=!variable.ripweaving
actions.bear+=/incarnation
actions.bear+=/berserk_bear
actions.bear+=/lunar_beam,if=(cooldown.incarnation_guardian_of_ursoc.remains>10+5*talent.ursocs_guidance.enabled|cooldown.berserk_bear.remains>10)&(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.ready|cooldown.rage_of_the_sleeper.remains>5)
actions.bear+=/rage_of_the_sleeper,if=(cooldown.incarnation_guardian_of_ursoc.remains>10+5*talent.ursocs_guidance.enabled|cooldown.berserk_bear.remains>10)&rage>40&(!talent.lunar_beam.enabled|cooldown.lunar_beam.remains>5)
actions.bear+=/berserking,if=buff.incarnation_guardian_of_ursoc.up|buff.berserk_bear.up

actions.bear+=/ironfur,if=!buff.ravage.up&variable.ironfur_spam&(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>1)
actions.bear+=/ironfur,if=!buff.ravage.up&rage.deficit<=(12+4*buff.gore.up+5*talent.soul_of_the_forest.enabled)<?(talent.blood_frenzy.enabled*3*(spell_targets.swipe_bear>?5))

actions.bear+=/maul,if=buff.ravage.up

actions.bear+=/raze,if=buff.tooth_and_claw.up&buff.tooth_and_claw.remains<1+gcd.max&spell_targets.swipe_bear>=2
actions.bear+=/maul,if=buff.tooth_and_claw.up&buff.tooth_and_claw.remains<1+gcd.max&spell_targets.swipe_bear<=5

actions.bear+=/call_action_list,name=preweave,if=variable.ripweaving&!buff.ravage.up&spell_targets.swipe_bear<=2&energy>60&(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains<=52)&buff.feline_potential_counter.stack=6
actions.bear+=/mangle,if=variable.ripweaving&spell_targets.swipe_bear<=2&buff.feline_potential_counter.stack<6&rage.deficit>12+4*buff.gore.up+5*talent.soul_of_the_forest.enabled

actions.bear+=/thrash_bear,if=spell_targets.swipe_bear>=5&rage.deficit>talent.blood_frenzy.enabled*15&talent.lunar_calling.enabled

actions.bear+=/call_action_list,name=generator,if=talent.ravage.enabled

actions.bear+=/raze,if=buff.tooth_and_claw.stack=2&(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>1)&spell_targets.swipe_bear>=2
actions.bear+=/maul,if=buff.tooth_and_claw.stack=2&(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>1)&spell_targets.swipe_bear<=5

actions.bear+=/raze,if=buff.vicious_cycle_maul.stack=3&(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>1)&spell_targets.swipe_bear>=2
actions.bear+=/maul,if=buff.vicious_cycle_maul.stack=3&(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>1)&spell_targets.swipe_bear<=5

actions.bear+=/raze,if=(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>1)&spell_targets.swipe_bear>=2
actions.bear+=/maul,if=(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>1)&spell_targets.swipe_bear<=5&!(talent.ravage.enabled&variable.ironfur_spam)

actions.bear+=/call_action_list,name=generator,if=!talent.ravage.enabled

actions.bear+=/rake,if=variable.use_cat_filler&(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains<=52)&spell_targets.swipe_bear<=2&energy>60&refreshable
actions.bear+=/shred,if=variable.use_cat_filler&(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains<=52)&spell_targets.swipe_bear<=2&energy>60&!dot.rake.refreshable

# actions.bear+=/call_action_list,name=preweave,if=variable.use_cat_filler&!buff.ravage.up&(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains<=52)&spell_targets.swipe_bear<=2&energy>60

actions.bear+=/starsurge,if=talent.starsurge.enabled&rage<20
actions.bear+=/moonfire,if=talent.lunar_insight.enabled&spell_targets.swipe_bear>=2&spell_targets.swipe_bear<=4
actions.bear+=/swipe_bear


actions.generator+=/mangle,if=buff.incarnation.up&spell_targets.swipe_bear>=2&spell_targets.swipe_bear<=10&rage.deficit>12+4*buff.gore.up+5*talent.soul_of_the_forest.enabled
actions.generator+=/mangle,if=buff.vicious_cycle_mangle.stack=3&spell_targets.swipe_bear<=10&rage.deficit>12+4*buff.gore.up+5*talent.soul_of_the_forest.enabled
actions.generator+=/mangle,if=buff.gore.up&spell_targets.swipe_bear<=10&rage.deficit>12+4*buff.gore.up+5*talent.soul_of_the_forest.enabled
actions.generator+=/thrash_bear,if=spell_targets.swipe_bear>=5&rage.deficit>talent.blood_frenzy.enabled*15
actions.generator+=/mangle,if=spell_targets.swipe_bear<=10&rage.deficit>12+4*buff.gore.up+5*talent.soul_of_the_forest.enabled
actions.generator+=/thrash_bear,if=spell_targets.swipe_bear>=1&spell_targets.swipe_bear<=4&rage.deficit>talent.blood_frenzy.enabled*3*spell_targets.swipe_bear
actions.generator+=/moonfire,if=buff.galactic_guardian.up&talent.boundless_moonlight.enabled&rage.deficit>8


actions.preweave+=/heart_of_the_wild,if=buff.feline_potential_counter.stack=6
actions.preweave+=/ironfur,if=variable.ironfur_spam
actions.preweave+=/rake,if=refreshable
actions.preweave+=/shred,if=!dot.rake.refreshable


actions.cat+=/convoke_the_spirits,if=buff.feline_potential.up
actions.cat+=/rip,if=buff.feline_potential.up&refreshable&target.time_to_die>12
actions.cat+=/ferocious_bite,if=buff.feline_potential.up&energy>50
actions.cat+=/rip,if=buff.feline_potential.down&!ticking&combo_points>=2&combo_points<=7-buff.feline_potential_counter.stack
actions.cat+=/ferocious_bite,if=buff.feline_potential.down&energy>50&combo_points=5
actions.cat+=/mangle,if=buff.feline_potential.down
actions.cat+=/shred,if=energy>60
