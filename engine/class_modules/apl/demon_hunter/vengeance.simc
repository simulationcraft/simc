# Check if fiery demise is active and spread
actions+=/variable,name=fd_ready,value=talent.fiery_brand&talent.fiery_demise&active_dot.fiery_brand>0

# Don't spend fury when fel dev soon to maximize fel dev uptime
actions+=/variable,name=dont_cleave,value=(cooldown.fel_devastation.remains<=(action.soul_cleave.execute_time+gcd.remains))&fury<80

# When to use Spirit Bomb with Focused Cleave
actions+=/variable,name=single_target,value=spell_targets.spirit_bomb=1
actions+=/variable,name=small_aoe,value=spell_targets.spirit_bomb>=2&spell_targets.spirit_bomb<=5
actions+=/variable,name=big_aoe,value=spell_targets.spirit_bomb>=6

actions+=/variable,name=can_spb,op=setif,condition=variable.fd_ready,value=(variable.single_target&soul_fragments>=5)|(variable.small_aoe&soul_fragments>=4)|(variable.big_aoe&soul_fragments>=3),value_else=(variable.small_aoe&soul_fragments>=5)|(variable.big_aoe&soul_fragments>=4)

# Executed before combat begins. Accepts non-harmful actions only.
actions.precombat=flask
actions.precombat+=/augmentation
actions.precombat+=/food
actions.precombat+=/snapshot_stats
actions.precombat+=/sigil_of_flame
actions.precombat+=/immolation_aura

actions+=/auto_attack
actions+=/disrupt,if=target.debuff.casting.react
actions+=/infernal_strike,use_off_gcd=1
actions+=/demon_spikes,use_off_gcd=1,if=!buff.demon_spikes.up&!cooldown.pause_action.remains
actions+=/metamorphosis,use_off_gcd=1,if=!buff.metamorphosis.up
actions+=/potion,use_off_gcd=1
actions+=/call_action_list,name=externals
actions+=/use_items,use_off_gcd=1
actions+=/call_action_list,name=fiery_demise,if=talent.fiery_brand&talent.fiery_demise&active_dot.fiery_brand>0
actions+=/call_action_list,name=maintenance
actions+=/run_action_list,name=single_target,if=variable.single_target
actions+=/run_action_list,name=small_aoe,if=variable.small_aoe
actions+=/run_action_list,name=big_aoe,if=variable.big_aoe

# Maintenance & upkeep
actions.maintenance=fiery_brand,if=talent.fiery_brand&((active_dot.fiery_brand=0&(cooldown.sigil_of_flame.remains<=(execute_time+gcd.remains)|cooldown.soul_carver.remains<=(execute_time+gcd.remains)|cooldown.fel_devastation.remains<=(execute_time+gcd.remains)))|(talent.down_in_flames&full_recharge_time<=(execute_time+gcd.remains)))
actions.maintenance+=/sigil_of_flame,if=talent.ascending_flame|active_dot.sigil_of_flame=0
actions.maintenance+=/immolation_aura
actions.maintenance+=/bulk_extraction,if=((5-soul_fragments)<=spell_targets)&soul_fragments<=2
actions.maintenance+=/spirit_bomb,if=variable.can_spb
actions.maintenance+=/felblade,if=(fury.deficit>=40&active_enemies=1)|((cooldown.fel_devastation.remains<=(execute_time+gcd.remains))&fury<50)
actions.maintenance+=/fracture,if=(cooldown.fel_devastation.remains<=(execute_time+gcd.remains))&fury<50
actions.maintenance+=/shear,if=(cooldown.fel_devastation.remains<=(execute_time+gcd.remains))&fury<50
# Don't overcap fury
actions.maintenance+=/spirit_bomb,if=fury.deficit<=30&spell_targets>1&soul_fragments>=4
actions.maintenance+=/soul_cleave,if=fury.deficit<=40

# Fiery demise window
actions.fiery_demise=immolation_aura
actions.fiery_demise+=/sigil_of_flame
actions.fiery_demise+=/felblade,if=(cooldown.fel_devastation.remains<=(execute_time+gcd.remains))&fury<50
actions.fiery_demise+=/fel_devastation
actions.fiery_demise+=/soul_carver,if=soul_fragments.total<3
actions.fiery_demise+=/the_hunt
actions.fiery_demise+=/elysian_decree,line_cd=1.85,if=fury>=40
actions.fiery_demise+=/spirit_bomb,if=variable.can_spb

# Single Target
actions.single_target=the_hunt
actions.single_target+=/soul_carver
actions.single_target+=/fel_devastation,if=talent.collective_anguish|(talent.stoke_the_flames&talent.burning_blood)
actions.single_target+=/elysian_decree
actions.single_target+=/fel_devastation
actions.single_target+=/soul_cleave,if=!variable.dont_cleave
actions.single_target+=/fracture
actions.single_target+=/shear
actions.single_target+=/call_action_list,name=filler

# 2-5 targets
actions.small_aoe=the_hunt
actions.small_aoe+=/fel_devastation,if=talent.collective_anguish.enabled|(talent.stoke_the_flames.enabled&talent.burning_blood.enabled)
actions.small_aoe+=/elysian_decree,line_cd=1.85,if=fury>=40&(soul_fragments.total<=1|soul_fragments.total>=4)
actions.small_aoe+=/fel_devastation
actions.small_aoe+=/soul_carver,if=soul_fragments.total<3
actions.small_aoe+=/soul_cleave,if=soul_fragments<=1&!variable.dont_cleave
actions.small_aoe+=/fracture
actions.small_aoe+=/shear
actions.small_aoe+=/call_action_list,name=filler

# 6+ targets
actions.big_aoe=fel_devastation,if=talent.collective_anguish|talent.stoke_the_flames
actions.big_aoe+=/the_hunt
actions.big_aoe+=/elysian_decree,line_cd=1.85,if=fury>=40&(soul_fragments.total<=1|soul_fragments.total>=4)
actions.big_aoe+=/fel_devastation
actions.big_aoe+=/soul_carver,if=soul_fragments.total<3
actions.big_aoe+=/spirit_bomb,if=soul_fragments>=4
actions.big_aoe+=/fracture
actions.big_aoe+=/shear
actions.big_aoe+=/soul_cleave,if=!variable.dont_cleave
actions.big_aoe+=/call_action_list,name=filler

# Filler
actions.filler=sigil_of_chains,if=talent.cycle_of_binding&talent.sigil_of_chains
actions.filler+=/sigil_of_misery,if=talent.cycle_of_binding&talent.sigil_of_misery
actions.filler+=/sigil_of_silence,if=talent.cycle_of_binding&talent.sigil_of_silence
actions.filler+=/felblade
actions.filler+=/throw_glaive

# External buffs
actions.externals=invoke_external_buff,name=symbol_of_hope
actions.externals+=/invoke_external_buff,name=power_infusion
